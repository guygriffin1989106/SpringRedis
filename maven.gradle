apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // respect 'optional' and 'provided' dependencies
        gradleProject.optionalDeps.each { dep ->
            generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
        }
        gradleProject.providedDeps.each { dep ->
            generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
        }

        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == 'test'
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = 'http://github.com/spring-projects/spring-data-redis'
            organization {
                name = 'Pivotal Software, Inc.'
                url = 'http://projects.spring.io/spring-data-redis'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url = 'http://github.com/spring-projects/spring-data-redis'
                connection = 'scm:git:git://github.com/spring-projects/spring-data-redis'
                developerConnection = 'scm:git:git://github.com/spring-projects/spring-data-redis'
            }
            developers {
                developer {
                    id = 'costin'
                    name = 'Costin Leau'
                    email = 'cleau@vmware.com'
                    properties {
                        twitter = 'costinl'
                    }
                }
                developer {
                    id = 'jencompgeek'
                    name = 'Jennifer Hickey'
                    properties {
                        twitter = 'jencompgeek'
                    }
                }
                developer {
                    id = 'christophstrobl'
                    name = 'Christoph Strobl'
                    properties {
                        twitter = 'stroblchristoph'
                    }
                }
                developer {
                    id = 'thomasdarimont'
                    name = 'Thomas Darimont'
                    properties {
                        twitter = 'thomasdarimont'
                    }
                }
            }
        }
    }
}