// used for artifact names, building doc upload urls, etc.
description = 'Spring Data Key Value'
abbreviation = 'DATAKV'

apply plugin: "eclipse"
apply plugin: "idea"
apply from: "$rootDir/gradle/docbook.gradle"

assemble.dependsOn generatePom

subprojects {
	apply plugin: "java"
	apply plugin: "maven"

	releaseType = "M2"
	version = "1.0.0.$releaseType"

	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:all"]

	// all core projects should be OSGi-compliant bundles
	// add the bundlor task to ensure proper manifests
	apply from: "$rootDir/gradle/bundlor.gradle"
	
	project.checkForProps = { Map args ->
		requiredPropSets.add args
	}
	// TODO: Finish integrating this stuff with our build
	//apply from: "$rootDir/gradle/maven-deployment.gradle"
	//apply from: "$rootDir/gradle/dist.gradle"

	repositories {
		// Read user's local Maven repo first
		mavenRepo name: "mavenLocal", urls: new File(System.getProperty("user.home"), ".m2/repository").toURL().toString()
		// Public Spring artefacts
		mavenRepo name: "spring-release", urls: "http://maven.springframework.org/release"
	  mavenRepo name: "springsource-org-release", urls: "http://repository.springsource.com/maven/bundles/release"
		mavenRepo name: "spring-milestone", urls: "http://maven.springframework.org/milestone"
		mavenRepo name: "spring-snapshot", urls: "http://maven.springframework.org/snapshot"
		// Additional community artefacts
		mavenCentral()
		mavenRepo name: "sonatype-snapshot", urls: "http://oss.sonatype.org/content/repositories/snapshots"
		mavenRepo name: "jboss", urls: "http://repository.jboss.org/maven2/"
		mavenRepo name: "java.net", urls: "http://download.java.net/maven/2/"
	}

	// Common dependencies
	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"
		compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
		runtime "log4j:log4j:$log4jVersion"
		runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
		// Spring Framework
		compile("org.springframework:spring-core:$springVersion") {
			exclude module: "commons-logging"
		}
		compile "org.springframework:spring-beans:$springVersion"
		compile "org.springframework:spring-context:$springVersion"
		compile "org.springframework:spring-context-support:$springVersion"
		compile "org.springframework:spring-tx:$springVersion"
		// Jackson JSON Mapper
		compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
		// Testing
		testCompile "junit:junit:$junitVersion"
		testCompile "org.springframework:spring-test:$springVersion"
		testCompile "org.mockito:mockito-all:$mockitoVersion"
	}
}

configurations {
	build
}

repositories {
	mavenCentral()
}

dependencies {
}

ideaProject {
	withXml { provider ->
		provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
	}
}