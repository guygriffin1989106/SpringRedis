// used for artifact names, building doc upload urls, etc.
description = 'Spring Data Redis'
abbreviation = 'DATAREDIS'

apply plugin: 'base'

buildscript {
    repositories {
//        add(new org.apache.ivy.plugins.resolver.FileSystemResolver()) {
//            name = "local"
//            addIvyPattern "e:/work/i21/gradle-plugins/build/repo/[organisation].[module]-ivy-[revision].xml"
//            addArtifactPattern "e:/work/i21/gradle-plugins/build/repo/[organisation].[module]-[revision](-[classifier]).[ext]"
//        }
    
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = "GitHub"
            addIvyPattern 'http://cloud.github.com/downloads/costin/gradle-stuff/[organization].[module]-[artifact]-[revision].[ext]'
            addArtifactPattern 'http://cloud.github.com/downloads/costin/gradle-stuff/[organization].[module]-[revision].[ext]'
        }
        mavenCentral()
        mavenLocal()
        mavenRepo name: "springsource-org-release", urls: "http://repository.springsource.com/maven/bundles/release"
        mavenRepo name: "springsource-org-external", urls: "http://repository.springsource.com/maven/bundles/external"
    }

    dependencies {
        classpath 'org.springframework:gradle-stuff:0.1-20110421'
        classpath 'net.sf.docbook:docbook-xsl:1.75.2:ns-resources@zip'
    }
}

repositories {
  mavenRepo name: "springsource", urls: "http://repo.springsource.org/libs-snapshot"
}


allprojects {
    group = 'org.springframework.data.redis'
    version = "$springDataRedisVersion"
    
    releaseBuild = version.endsWith('RELEASE')
    snapshotBuild = version.endsWith('SNAPSHOT')


    repositories {
        mavenLocal()
        mavenCentral()
        // Public Spring artefacts
        mavenRepo name: "springsource-org-release", urls: "http://repository.springsource.com/maven/bundles/release"
        mavenRepo name: "spring-release", urls: "http://maven.springframework.org/release"
        mavenRepo name: "spring-milestone", urls: "http://maven.springframework.org/milestone"
        mavenRepo name: "spring-snapshot", urls: "http://maven.springframework.org/snapshot"
        mavenRepo name: "sonatype-snapshot", urls: "http://oss.sonatype.org/content/repositories/snapshots"
        mavenRepo name: "jboss", urls: "http://repository.jboss.org/maven2/"
        mavenRepo name: "java.net", urls: "http://download.java.net/maven/2/"
        mavenRepo name: "ext-snapshots", urls: "http://springframework.svn.sourceforge.net/svnroot/springframework/repos/repo-ext/"
	}

}


apply plugin: "java"
apply plugin: "maven"
apply plugin: 'eclipse'  // `gradle eclipse` to generate .classpath/.project
apply plugin: 'idea'     // `gradle idea` to generate .ipr/.iml
apply plugin: 'docbook'
apply plugin: 'bundlor'  // all core projects should be OSGi-compliant

bundlor.useProjectProps = true
[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:-serial"]

// Common dependencies
dependencies {
    // Logging
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    runtime "log4j:log4j:$log4jVersion"
    runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
    
    // Spring Framework
    compile("org.springframework:spring-core:$springVersion") {
        exclude module: "commons-logging"
    }
    compile "org.springframework:spring-context-support:$springVersion"
    compile "org.springframework:spring-tx:$springVersion"
    compile("org.springframework:spring-oxm:$springVersion") { optional = true } 

    // Redis Drivers
    compile "redis.clients:jedis:$jedisVersion"
    compile("org.jredis:jredis-anthonylauzon:$jredisVersion") { optional = true } 
    compile("org.idevlab:rjc:$rjcVersion") { optional = true } 
    
    // Mappers
    compile("org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion") { optional = true }
    compile("commons-beanutils:commons-beanutils-core:1.8.3") { optional = true } 

    // Testing
    testCompile "junit:junit:$junitVersion"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "org.mockito:mockito-all:$mockitoVersion"
    testCompile("javax.annotation:jsr250-api:1.0") { optional = true }
    testCompile("com.thoughtworks.xstream:xstream:1.3") { optional = true } 
}

javaprojects = rootProject

sourceCompatibility = 1.5
targetCompatibility = 1.5 

javadoc {
  srcDir = file("${projectDir}/docs/src/api")
  destinationDir = file("${buildDir}/api")
  tmpDir = file("${buildDir}/api-work")
  
  configure(options) {
      stylesheetFile = file("${srcDir}/spring-javadoc.css")
      overview = "${srcDir}/overview.html"
      docFilesSubDirs = true
      outputLevel = org.gradle.external.javadoc.JavadocOutputLevel.QUIET
      breakIterator = true
      showFromProtected()
      groups = [
        'Spring Data Redis Support' : ['org.springframework.data.redis*'],
      ]
  
     links = [
        "http://static.springframework.org/spring/docs/3.0.x/javadoc-api",
        "http://download.oracle.com/javase/6/docs/api",
        "http://jackson.codehaus.org/1.8.2/javadoc"
     ]
     
     exclude "org/springframework/data/redis/config/**"
  }
    
  title = "${rootProject.description} ${version} API"

  // collect all the sources that will be included in the javadoc output
  source javaprojects.collect {project ->
      project.sourceSets.main.allJava
  }

  // collect all main classpaths to be able to resolve @see refs, etc.
  // this collection also determines the set of projects that this
  // task dependsOn, thus the runtimeClasspath is used to ensure all
  // projects are included, not just *dependencies* of all classes.
  // this is awkward and took me a while to figure out.
  classpath = files(javaprojects.collect {project ->
      project.sourceSets.main.runtimeClasspath
  })

  // copy the images from the doc-files dir over to the target
  doLast { task ->
      copy {
          from file("${task.srcDir}/doc-files")
          into file("${task.destinationDir}/doc-files")
      }
  }
}

ideaProject {
	withXml { provider ->
		provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-3'
    description = "Generate the Gradle wrapper"
    group = "Distribution"
}

apply from: "$rootDir/maven.gradle"

assemble.dependsOn = ['jar', 'sourceJar', 'javadocJar']

// Distribution tasks
task dist(type: Zip) {
    description = "Generate the ZIP Distribution"
    group = "Distribution"
    dependsOn assemble, subprojects*.tasks*.matching { task -> task.name == 'assemble' }

    evaluationDependsOn(':docs')

    def zipRootDir = "${project.name}-$version"
    into(zipRootDir) {
        from('/docs/src/info') {
            include '*.txt'
        }
        from('/docs/build/') {
            into 'docs'
            include 'reference/**/*'
        }
        from('build/') {
            into 'docs'
            include 'api/**/*'
        }
            into('dist') {
            from javaprojects.collect {project -> project.libsDir }
        }
    }
    doLast {
        ant.checksum(file: archivePath, algorithm: 'SHA1', fileext: '.sha1')
    }
}

task uploadDist(type: org.springframework.gradle.tasks.S3DistroUpload, dependsOn: dist) {
    description = "Upload the ZIP Distribution"
    group = "Distribution"
    archiveFile = dist.archivePath
    projectKey = 'DATAKV'
    projectName = 'Spring Data Redis'
}

defaultTasks 'clean', 'build'